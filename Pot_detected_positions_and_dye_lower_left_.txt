// Made by Yano
// Plot detected soma and dye the lower left red

// Select image directory
dir = getDirectory("Choose an image directory");

// Get file list
fileList = getFileList(dir);

// Get the name of an image at each position
substractedName = newArray(fileList.length/2);
imageNumber = 0;
for(i = 0; i < fileList.length; i++){
    if(endsWith(fileList[i], ".tiff")){
        index = indexOf(fileList[i], "_");
        substractedName[imageNumber] = substring(fileList[i], 0, index);
        imageNumber = imageNumber + 1;
    }
}

for(i = 0; i < substractedName.length; i++){
    // Open image and detected position
    for(ii = 0; ii < fileList.length; ii++){
        if(startsWith(fileList[ii], substractedName[i])){
            run("Open...", "path=" + dir + "\\" + fileList[ii]);
        }
    }       
    
    // Change image type. 8bit color -> RGB color
    run("RGB Color");
    
    // Set color to show the location of soma
    setForegroundColor(0, 0, 255); // Blue

    x = newArray(nResults);
    y = newArray(nResults);
    for(ii = 0; ii < nResults; ii++){
        x[ii] = getResult("DetectedX", ii);
        y[ii] = getResult("DetectedY", ii);
    }
            
    // plot the center of soma
    for(ii = 0; ii < x.length; ii++){
                   
        // Select the center of soma
        run("Specify...", "width=10 height=10 x=" + x[ii] + " y=" + y[ii] + " oval constrain centered");

        // Coloring the center of soma
        run("Fill", "slice"); 
    }
    
    // Set color to show the the upper left
    setForegroundColor(255, 0, 0); // Red
    
    // Dye the upper left of the image red
    run("Specify...", "width=20 height=20 x=" + 0 + " y=" + 0 + " oval constrain centered");
    // Coloring the center of soma
    run("Fill", "slice"); 
    
    // Restore the file type. RGB color -> 8bit color
    run("8-bit Color", "number=256");
    
    // Save completed image and close
    run("Save");
    run("Close All");
}